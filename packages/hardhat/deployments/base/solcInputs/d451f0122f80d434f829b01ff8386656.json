{
  "language": "Solidity",
  "sources": {
    "@openzeppelin/contracts/access/Ownable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v5.0.0) (access/Ownable.sol)\n\npragma solidity ^0.8.20;\n\nimport {Context} from \"../utils/Context.sol\";\n\n/**\n * @dev Contract module which provides a basic access control mechanism, where\n * there is an account (an owner) that can be granted exclusive access to\n * specific functions.\n *\n * The initial owner is set to the address provided by the deployer. This can\n * later be changed with {transferOwnership}.\n *\n * This module is used through inheritance. It will make available the modifier\n * `onlyOwner`, which can be applied to your functions to restrict their use to\n * the owner.\n */\nabstract contract Ownable is Context {\n    address private _owner;\n\n    /**\n     * @dev The caller account is not authorized to perform an operation.\n     */\n    error OwnableUnauthorizedAccount(address account);\n\n    /**\n     * @dev The owner is not a valid owner account. (eg. `address(0)`)\n     */\n    error OwnableInvalidOwner(address owner);\n\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\n\n    /**\n     * @dev Initializes the contract setting the address provided by the deployer as the initial owner.\n     */\n    constructor(address initialOwner) {\n        if (initialOwner == address(0)) {\n            revert OwnableInvalidOwner(address(0));\n        }\n        _transferOwnership(initialOwner);\n    }\n\n    /**\n     * @dev Throws if called by any account other than the owner.\n     */\n    modifier onlyOwner() {\n        _checkOwner();\n        _;\n    }\n\n    /**\n     * @dev Returns the address of the current owner.\n     */\n    function owner() public view virtual returns (address) {\n        return _owner;\n    }\n\n    /**\n     * @dev Throws if the sender is not the owner.\n     */\n    function _checkOwner() internal view virtual {\n        if (owner() != _msgSender()) {\n            revert OwnableUnauthorizedAccount(_msgSender());\n        }\n    }\n\n    /**\n     * @dev Leaves the contract without owner. It will not be possible to call\n     * `onlyOwner` functions. Can only be called by the current owner.\n     *\n     * NOTE: Renouncing ownership will leave the contract without an owner,\n     * thereby disabling any functionality that is only available to the owner.\n     */\n    function renounceOwnership() public virtual onlyOwner {\n        _transferOwnership(address(0));\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Can only be called by the current owner.\n     */\n    function transferOwnership(address newOwner) public virtual onlyOwner {\n        if (newOwner == address(0)) {\n            revert OwnableInvalidOwner(address(0));\n        }\n        _transferOwnership(newOwner);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Internal function without access restriction.\n     */\n    function _transferOwnership(address newOwner) internal virtual {\n        address oldOwner = _owner;\n        _owner = newOwner;\n        emit OwnershipTransferred(oldOwner, newOwner);\n    }\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/extensions/IERC20Permit.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/extensions/IERC20Permit.sol)\n\npragma solidity ^0.8.20;\n\n/**\n * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in\n * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].\n *\n * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by\n * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't\n * need to send a transaction, and thus is not required to hold Ether at all.\n *\n * ==== Security Considerations\n *\n * There are two important considerations concerning the use of `permit`. The first is that a valid permit signature\n * expresses an allowance, and it should not be assumed to convey additional meaning. In particular, it should not be\n * considered as an intention to spend the allowance in any specific way. The second is that because permits have\n * built-in replay protection and can be submitted by anyone, they can be frontrun. A protocol that uses permits should\n * take this into consideration and allow a `permit` call to fail. Combining these two aspects, a pattern that may be\n * generally recommended is:\n *\n * ```solidity\n * function doThingWithPermit(..., uint256 value, uint256 deadline, uint8 v, bytes32 r, bytes32 s) public {\n *     try token.permit(msg.sender, address(this), value, deadline, v, r, s) {} catch {}\n *     doThing(..., value);\n * }\n *\n * function doThing(..., uint256 value) public {\n *     token.safeTransferFrom(msg.sender, address(this), value);\n *     ...\n * }\n * ```\n *\n * Observe that: 1) `msg.sender` is used as the owner, leaving no ambiguity as to the signer intent, and 2) the use of\n * `try/catch` allows the permit to fail and makes the code tolerant to frontrunning. (See also\n * {SafeERC20-safeTransferFrom}).\n *\n * Additionally, note that smart contract wallets (such as Argent or Safe) are not able to produce permit signatures, so\n * contracts should have entry points that don't rely on permit.\n */\ninterface IERC20Permit {\n    /**\n     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,\n     * given ``owner``'s signed approval.\n     *\n     * IMPORTANT: The same issues {IERC20-approve} has related to transaction\n     * ordering also apply here.\n     *\n     * Emits an {Approval} event.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     * - `deadline` must be a timestamp in the future.\n     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`\n     * over the EIP712-formatted function arguments.\n     * - the signature must use ``owner``'s current nonce (see {nonces}).\n     *\n     * For more information on the signature format, see the\n     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP\n     * section].\n     *\n     * CAUTION: See Security Considerations above.\n     */\n    function permit(\n        address owner,\n        address spender,\n        uint256 value,\n        uint256 deadline,\n        uint8 v,\n        bytes32 r,\n        bytes32 s\n    ) external;\n\n    /**\n     * @dev Returns the current nonce for `owner`. This value must be\n     * included whenever a signature is generated for {permit}.\n     *\n     * Every successful call to {permit} increases ``owner``'s nonce by one. This\n     * prevents a signature from being used multiple times.\n     */\n    function nonces(address owner) external view returns (uint256);\n\n    /**\n     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.\n     */\n    // solhint-disable-next-line func-name-mixedcase\n    function DOMAIN_SEPARATOR() external view returns (bytes32);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/IERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/IERC20.sol)\n\npragma solidity ^0.8.20;\n\n/**\n * @dev Interface of the ERC20 standard as defined in the EIP.\n */\ninterface IERC20 {\n    /**\n     * @dev Emitted when `value` tokens are moved from one account (`from`) to\n     * another (`to`).\n     *\n     * Note that `value` may be zero.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 value);\n\n    /**\n     * @dev Emitted when the allowance of a `spender` for an `owner` is set by\n     * a call to {approve}. `value` is the new allowance.\n     */\n    event Approval(address indexed owner, address indexed spender, uint256 value);\n\n    /**\n     * @dev Returns the value of tokens in existence.\n     */\n    function totalSupply() external view returns (uint256);\n\n    /**\n     * @dev Returns the value of tokens owned by `account`.\n     */\n    function balanceOf(address account) external view returns (uint256);\n\n    /**\n     * @dev Moves a `value` amount of tokens from the caller's account to `to`.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transfer(address to, uint256 value) external returns (bool);\n\n    /**\n     * @dev Returns the remaining number of tokens that `spender` will be\n     * allowed to spend on behalf of `owner` through {transferFrom}. This is\n     * zero by default.\n     *\n     * This value changes when {approve} or {transferFrom} are called.\n     */\n    function allowance(address owner, address spender) external view returns (uint256);\n\n    /**\n     * @dev Sets a `value` amount of tokens as the allowance of `spender` over the\n     * caller's tokens.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * IMPORTANT: Beware that changing an allowance with this method brings the risk\n     * that someone may use both the old and the new allowance by unfortunate\n     * transaction ordering. One possible solution to mitigate this race\n     * condition is to first reduce the spender's allowance to 0 and set the\n     * desired value afterwards:\n     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address spender, uint256 value) external returns (bool);\n\n    /**\n     * @dev Moves a `value` amount of tokens from `from` to `to` using the\n     * allowance mechanism. `value` is then deducted from the caller's\n     * allowance.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(address from, address to, uint256 value) external returns (bool);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/utils/SafeERC20.sol)\n\npragma solidity ^0.8.20;\n\nimport {IERC20} from \"../IERC20.sol\";\nimport {IERC20Permit} from \"../extensions/IERC20Permit.sol\";\nimport {Address} from \"../../../utils/Address.sol\";\n\n/**\n * @title SafeERC20\n * @dev Wrappers around ERC20 operations that throw on failure (when the token\n * contract returns false). Tokens that return no value (and instead revert or\n * throw on failure) are also supported, non-reverting calls are assumed to be\n * successful.\n * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,\n * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.\n */\nlibrary SafeERC20 {\n    using Address for address;\n\n    /**\n     * @dev An operation with an ERC20 token failed.\n     */\n    error SafeERC20FailedOperation(address token);\n\n    /**\n     * @dev Indicates a failed `decreaseAllowance` request.\n     */\n    error SafeERC20FailedDecreaseAllowance(address spender, uint256 currentAllowance, uint256 requestedDecrease);\n\n    /**\n     * @dev Transfer `value` amount of `token` from the calling contract to `to`. If `token` returns no value,\n     * non-reverting calls are assumed to be successful.\n     */\n    function safeTransfer(IERC20 token, address to, uint256 value) internal {\n        _callOptionalReturn(token, abi.encodeCall(token.transfer, (to, value)));\n    }\n\n    /**\n     * @dev Transfer `value` amount of `token` from `from` to `to`, spending the approval given by `from` to the\n     * calling contract. If `token` returns no value, non-reverting calls are assumed to be successful.\n     */\n    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {\n        _callOptionalReturn(token, abi.encodeCall(token.transferFrom, (from, to, value)));\n    }\n\n    /**\n     * @dev Increase the calling contract's allowance toward `spender` by `value`. If `token` returns no value,\n     * non-reverting calls are assumed to be successful.\n     */\n    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {\n        uint256 oldAllowance = token.allowance(address(this), spender);\n        forceApprove(token, spender, oldAllowance + value);\n    }\n\n    /**\n     * @dev Decrease the calling contract's allowance toward `spender` by `requestedDecrease`. If `token` returns no\n     * value, non-reverting calls are assumed to be successful.\n     */\n    function safeDecreaseAllowance(IERC20 token, address spender, uint256 requestedDecrease) internal {\n        unchecked {\n            uint256 currentAllowance = token.allowance(address(this), spender);\n            if (currentAllowance < requestedDecrease) {\n                revert SafeERC20FailedDecreaseAllowance(spender, currentAllowance, requestedDecrease);\n            }\n            forceApprove(token, spender, currentAllowance - requestedDecrease);\n        }\n    }\n\n    /**\n     * @dev Set the calling contract's allowance toward `spender` to `value`. If `token` returns no value,\n     * non-reverting calls are assumed to be successful. Meant to be used with tokens that require the approval\n     * to be set to zero before setting it to a non-zero value, such as USDT.\n     */\n    function forceApprove(IERC20 token, address spender, uint256 value) internal {\n        bytes memory approvalCall = abi.encodeCall(token.approve, (spender, value));\n\n        if (!_callOptionalReturnBool(token, approvalCall)) {\n            _callOptionalReturn(token, abi.encodeCall(token.approve, (spender, 0)));\n            _callOptionalReturn(token, approvalCall);\n        }\n    }\n\n    /**\n     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement\n     * on the return value: the return value is optional (but if data is returned, it must not be false).\n     * @param token The token targeted by the call.\n     * @param data The call data (encoded using abi.encode or one of its variants).\n     */\n    function _callOptionalReturn(IERC20 token, bytes memory data) private {\n        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since\n        // we're implementing it ourselves. We use {Address-functionCall} to perform this call, which verifies that\n        // the target address contains contract code and also asserts for success in the low-level call.\n\n        bytes memory returndata = address(token).functionCall(data);\n        if (returndata.length != 0 && !abi.decode(returndata, (bool))) {\n            revert SafeERC20FailedOperation(address(token));\n        }\n    }\n\n    /**\n     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement\n     * on the return value: the return value is optional (but if data is returned, it must not be false).\n     * @param token The token targeted by the call.\n     * @param data The call data (encoded using abi.encode or one of its variants).\n     *\n     * This is a variant of {_callOptionalReturn} that silents catches all reverts and returns a bool instead.\n     */\n    function _callOptionalReturnBool(IERC20 token, bytes memory data) private returns (bool) {\n        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since\n        // we're implementing it ourselves. We cannot use {Address-functionCall} here since this should return false\n        // and not revert is the subcall reverts.\n\n        (bool success, bytes memory returndata) = address(token).call(data);\n        return success && (returndata.length == 0 || abi.decode(returndata, (bool))) && address(token).code.length > 0;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Address.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v5.0.0) (utils/Address.sol)\n\npragma solidity ^0.8.20;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev The ETH balance of the account is not enough to perform the operation.\n     */\n    error AddressInsufficientBalance(address account);\n\n    /**\n     * @dev There's no code at `target` (it is not a contract).\n     */\n    error AddressEmptyCode(address target);\n\n    /**\n     * @dev A call to an address target failed. The target may have reverted.\n     */\n    error FailedInnerCall();\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.8.20/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        if (address(this).balance < amount) {\n            revert AddressInsufficientBalance(address(this));\n        }\n\n        (bool success, ) = recipient.call{value: amount}(\"\");\n        if (!success) {\n            revert FailedInnerCall();\n        }\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain `call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason or custom error, it is bubbled\n     * up by this function (like regular Solidity function calls). However, if\n     * the call reverted with no returned reason, this function reverts with a\n     * {FailedInnerCall} error.\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {\n        if (address(this).balance < value) {\n            revert AddressInsufficientBalance(address(this));\n        }\n        (bool success, bytes memory returndata) = target.call{value: value}(data);\n        return verifyCallResultFromTarget(target, success, returndata);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return verifyCallResultFromTarget(target, success, returndata);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a delegate call.\n     */\n    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {\n        (bool success, bytes memory returndata) = target.delegatecall(data);\n        return verifyCallResultFromTarget(target, success, returndata);\n    }\n\n    /**\n     * @dev Tool to verify that a low level call to smart-contract was successful, and reverts if the target\n     * was not a contract or bubbling up the revert reason (falling back to {FailedInnerCall}) in case of an\n     * unsuccessful call.\n     */\n    function verifyCallResultFromTarget(\n        address target,\n        bool success,\n        bytes memory returndata\n    ) internal view returns (bytes memory) {\n        if (!success) {\n            _revert(returndata);\n        } else {\n            // only check if target is a contract if the call was successful and the return data is empty\n            // otherwise we already know that it was a contract\n            if (returndata.length == 0 && target.code.length == 0) {\n                revert AddressEmptyCode(target);\n            }\n            return returndata;\n        }\n    }\n\n    /**\n     * @dev Tool to verify that a low level call was successful, and reverts if it wasn't, either by bubbling the\n     * revert reason or with a default {FailedInnerCall} error.\n     */\n    function verifyCallResult(bool success, bytes memory returndata) internal pure returns (bytes memory) {\n        if (!success) {\n            _revert(returndata);\n        } else {\n            return returndata;\n        }\n    }\n\n    /**\n     * @dev Reverts with returndata if present. Otherwise reverts with {FailedInnerCall}.\n     */\n    function _revert(bytes memory returndata) private pure {\n        // Look for revert reason and bubble it up if present\n        if (returndata.length > 0) {\n            // The easiest way to bubble the revert reason is using memory via assembly\n            /// @solidity memory-safe-assembly\n            assembly {\n                let returndata_size := mload(returndata)\n                revert(add(32, returndata), returndata_size)\n            }\n        } else {\n            revert FailedInnerCall();\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Context.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v5.0.1) (utils/Context.sol)\n\npragma solidity ^0.8.20;\n\n/**\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\nabstract contract Context {\n    function _msgSender() internal view virtual returns (address) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view virtual returns (bytes calldata) {\n        return msg.data;\n    }\n\n    function _contextSuffixLength() internal view virtual returns (uint256) {\n        return 0;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/ReentrancyGuard.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v5.0.0) (utils/ReentrancyGuard.sol)\n\npragma solidity ^0.8.20;\n\n/**\n * @dev Contract module that helps prevent reentrant calls to a function.\n *\n * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier\n * available, which can be applied to functions to make sure there are no nested\n * (reentrant) calls to them.\n *\n * Note that because there is a single `nonReentrant` guard, functions marked as\n * `nonReentrant` may not call one another. This can be worked around by making\n * those functions `private`, and then adding `external` `nonReentrant` entry\n * points to them.\n *\n * TIP: If you would like to learn more about reentrancy and alternative ways\n * to protect against it, check out our blog post\n * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].\n */\nabstract contract ReentrancyGuard {\n    // Booleans are more expensive than uint256 or any type that takes up a full\n    // word because each write operation emits an extra SLOAD to first read the\n    // slot's contents, replace the bits taken up by the boolean, and then write\n    // back. This is the compiler's defense against contract upgrades and\n    // pointer aliasing, and it cannot be disabled.\n\n    // The values being non-zero value makes deployment a bit more expensive,\n    // but in exchange the refund on every call to nonReentrant will be lower in\n    // amount. Since refunds are capped to a percentage of the total\n    // transaction's gas, it is best to keep them low in cases like this one, to\n    // increase the likelihood of the full refund coming into effect.\n    uint256 private constant NOT_ENTERED = 1;\n    uint256 private constant ENTERED = 2;\n\n    uint256 private _status;\n\n    /**\n     * @dev Unauthorized reentrant call.\n     */\n    error ReentrancyGuardReentrantCall();\n\n    constructor() {\n        _status = NOT_ENTERED;\n    }\n\n    /**\n     * @dev Prevents a contract from calling itself, directly or indirectly.\n     * Calling a `nonReentrant` function from another `nonReentrant`\n     * function is not supported. It is possible to prevent this from happening\n     * by making the `nonReentrant` function external, and making it call a\n     * `private` function that does the actual work.\n     */\n    modifier nonReentrant() {\n        _nonReentrantBefore();\n        _;\n        _nonReentrantAfter();\n    }\n\n    function _nonReentrantBefore() private {\n        // On the first call to nonReentrant, _status will be NOT_ENTERED\n        if (_status == ENTERED) {\n            revert ReentrancyGuardReentrantCall();\n        }\n\n        // Any calls to nonReentrant after this point will fail\n        _status = ENTERED;\n    }\n\n    function _nonReentrantAfter() private {\n        // By storing the original value once again, a refund is triggered (see\n        // https://eips.ethereum.org/EIPS/eip-2200)\n        _status = NOT_ENTERED;\n    }\n\n    /**\n     * @dev Returns true if the reentrancy guard is currently set to \"entered\", which indicates there is a\n     * `nonReentrant` function in the call stack.\n     */\n    function _reentrancyGuardEntered() internal view returns (bool) {\n        return _status == ENTERED;\n    }\n}\n"
    },
    "contracts/v2/flashloans/FlashLoanConsumerBase.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.30;\n\nimport {IERC20} from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport {SafeERC20} from \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\n\n// --- Existing Balancer imports ---\nimport {IFlashLoanProvider} from \"../interfaces/balancer/IFlashLoanProvider.sol\";\nimport {IVaultV3} from \"../interfaces/balancer/IVaultV3.sol\";\n\n// --- Minimal Aave v3 interfaces (flashLoanSimple) ---\ninterface IAaveV3Pool {\n    function flashLoanSimple(\n        address receiverAddress,\n        address asset,\n        uint256 amount,\n        bytes calldata params,\n        uint16 referralCode\n    ) external;\n}\n\ninterface IAaveFlashLoanSimpleReceiver {\n    function executeOperation(\n        address asset,\n        uint256 amount,\n        uint256 premium,\n        address initiator,\n        bytes calldata params\n    ) external returns (bool);\n}\n\n// --- Minimal Uniswap v3 pool interface ---\ninterface IUniswapV3Pool {\n    function token0() external view returns (address);\n    function token1() external view returns (address);\n    function flash(address recipient, uint256 amount0, uint256 amount1, bytes calldata data) external;\n}\n\n/// @notice Abstract base supporting Balancer v2/v3, Aave v3 and Uniswap v3 flash sources.\n///         Providers are *configurable*; zero address = disabled (and requests revert).\nabstract contract FlashLoanConsumerBase is IAaveFlashLoanSimpleReceiver {\n    using SafeERC20 for IERC20;\n\n    // ----------------- Providers (config) -----------------\n    IFlashLoanProvider public balancerV2Vault; // Balancer v2 Vault\n    IVaultV3 public balancerV3Vault;           // Balancer v3 Vault\n    IAaveV3Pool public aaveV3Pool;             // Aave v3 Pool (flashLoanSimple)\n\n    // We gate Uniswap v3 availability by storing a *factory or sentinel address*.\n    // The actual pool is passed per-request.\n    address public uniswapV3FactoryOrSentinel;\n\n    // ----------------- Internal state guard -----------------\n    bool private flashLoanEnabled;\n    address private authorizedFlashSender;\n\n    // ----------------- Events (optional, helpful for tracing) -----------------\n    event FlashRequested(bytes32 provider, address indexed token, uint256 amount);\n    event FlashRepaid(bytes32 provider, address indexed token, uint256 amountWithFee);\n\n    // ----------------- Setters -----------------\n    function _setBalancerV2(address provider) internal {\n        balancerV2Vault = IFlashLoanProvider(provider);\n    }\n\n    function _setBalancerV3(address vault) internal {\n        balancerV3Vault = IVaultV3(vault);\n    }\n\n    function _setAaveV3(address pool) internal {\n        aaveV3Pool = IAaveV3Pool(pool);\n    }\n\n    /// @dev Any non-zero address works as an enable switch (factory, router, sentinel).\n    function _setUniswapV3Enabled(address factoryOrSentinel) internal {\n        uniswapV3FactoryOrSentinel = factoryOrSentinel;\n    }\n\n    // ----------------- Enabled flags -----------------\n    function balancerV2Enabled() public view returns (bool) {\n        return address(balancerV2Vault) != address(0);\n    }\n\n    function balancerV3Enabled() public view returns (bool) {\n        return address(balancerV3Vault) != address(0);\n    }\n\n    function aaveEnabled() public view returns (bool) {\n        return address(aaveV3Pool) != address(0);\n    }\n\n    function uniswapEnabled() public view returns (bool) {\n        return uniswapV3FactoryOrSentinel != address(0);\n    }\n\n    // ----------------- Guard modifiers -----------------\n    modifier enableFlashLoan() {\n        flashLoanEnabled = true;\n        _;\n        flashLoanEnabled = false;\n    }\n\n    modifier flashLoanOnly() {\n        require(flashLoanEnabled, \"Flash loan not enabled\");\n        require(msg.sender == authorizedFlashSender, \"Unauthorized flash caller\");\n        _;\n    }\n\n    modifier authorizeFlashLoan(address expectedSender) {\n        flashLoanEnabled = true;\n        authorizedFlashSender = expectedSender;\n        _;\n        authorizedFlashSender = address(0);\n        flashLoanEnabled = false;\n    }\n\n    // ============================================================\n    //                          BALANCER V2\n    // ============================================================\n\n    /// @notice Request a Balancer v2 flash loan (single-asset for simplicity).\n    function _requestBalancerV2(address token, uint256 amount, bytes memory userData) internal authorizeFlashLoan(address(balancerV2Vault)) {\n        require(balancerV2Enabled(), \"Balancer v2 disabled\");\n\n        IERC20[] memory tokens = new IERC20[](1);\n        tokens[0] = IERC20(token);\n        uint256[] memory amounts = new uint256[](1);\n        amounts[0] = amount;\n\n        emit FlashRequested(\"BALANCER_V2\", token, amount);\n        balancerV2Vault.flashLoan(address(this), tokens, amounts, userData);\n    }\n\n    /// @notice Balancer v2 callback.\n    function receiveFlashLoan(\n        IERC20[] calldata tokens,\n        uint256[] calldata amounts,\n        uint256[] calldata feeAmounts,\n        bytes calldata userData\n    ) external flashLoanOnly {\n        require(msg.sender == address(balancerV2Vault), \"Unauthorized Balancer v2\");\n\n        // For single-asset flash loans, pass the repayment amount\n        require(tokens.length == 1, \"Only single-asset flash loans supported\");\n        uint256 repayment = amounts[0] + feeAmounts[0];\n        _afterFlashLoan(address(tokens[0]), repayment, userData);\n\n        // Repay principal + fee to the vault\n        tokens[0].safeTransfer(address(balancerV2Vault), repayment);\n        emit FlashRepaid(\"BALANCER_V2\", address(tokens[0]), repayment);\n    }\n\n    // ============================================================\n    //                          BALANCER V3\n    // ============================================================\n\n    /// @notice Request a Balancer v3 \"unlocked\" context; vault will callback with our calldata.\n    function _requestBalancerV3(address token, uint256 amount) internal authorizeFlashLoan(address(balancerV3Vault)) {\n        require(balancerV3Enabled(), \"Balancer v3 disabled\");\n\n        bytes memory userData = abi.encode(token, amount);\n        bytes memory callData = abi.encodeWithSelector(this.receiveFlashLoanV3.selector, userData);\n\n        emit FlashRequested(\"BALANCER_V3\", token, amount);\n        balancerV3Vault.unlock(callData); // calls back receiveFlashLoanV3 within unlocked context\n    }\n\n    /// @notice Balancer v3 callback, runs in unlocked context.\n    function receiveFlashLoanV3(bytes calldata userData) external flashLoanOnly {\n        require(msg.sender == address(balancerV3Vault), \"Unauthorized Balancer v3\");\n\n        (address token, uint256 amount) = abi.decode(userData, (address, uint256));\n\n        if (token != address(0) && amount > 0) {\n            // Pull funds in unlocked context\n            balancerV3Vault.sendTo(token, address(this), amount);\n            // For Balancer v3, repayment amount equals the borrowed amount (fee handled by vault accounting)\n            _afterFlashLoan(token, amount, userData);\n            // Repay + settle (no premium encoded here, handled by vault accounting)\n            IERC20(token).safeTransfer(address(balancerV3Vault), amount);\n            balancerV3Vault.settle(token, amount);\n            emit FlashRepaid(\"BALANCER_V3\", token, amount);\n        }\n    }\n\n    // ============================================================\n    //                            AAVE V3\n    // ============================================================\n\n    /// @notice Request an Aave v3 flash loan (simple mode).\n    /// @param token Asset to borrow\n    /// @param amount Amount to borrow\n    /// @param userData Arbitrary data forwarded to executeOperation and to _afterFlashLoan\n    function _requestAaveV3(address token, uint256 amount, bytes memory userData) internal authorizeFlashLoan(address(aaveV3Pool)) {\n        require(aaveEnabled(), \"Aave v3 disabled\");\n        emit FlashRequested(\"AAVE_V3\", token, amount);\n        // referralCode = 0\n        aaveV3Pool.flashLoanSimple(address(this), token, amount, userData, 0);\n    }\n\n    /// @notice Aave v3 callback. Approve Pool for principal+premium; return true to finalize.\n    function executeOperation(\n        address asset,\n        uint256 amount,\n        uint256 premium,\n        address initiator,\n        bytes calldata params\n    ) external override flashLoanOnly returns (bool) {\n        require(msg.sender == address(aaveV3Pool), \"Unauthorized Aave pool\");\n        require(initiator == address(this), \"Bad initiator\");\n\n        uint256 repayment = amount + premium;\n        _afterFlashLoan(asset, repayment, params);\n\n        // Repay: approve Pool to pull principal + premium\n        // Reset approval to 0 first, then approve new amount (safeApprove pattern for compatibility)\n        IERC20(asset).approve(address(aaveV3Pool), 0);\n        IERC20(asset).approve(address(aaveV3Pool), repayment);\n\n        emit FlashRepaid(\"AAVE_V3\", asset, repayment);\n        return true;\n    }\n\n    // ============================================================\n    //                          UNISWAP V3\n    // ============================================================\n\n    /// @notice Request a Uniswap v3 flash (flash-swap) from a specific pool.\n    ///         We enforce global enable via uniswapV3FactoryOrSentinel (non-zero).\n    /// @param pool Address of the Uniswap v3 pool (must contain `token`)\n    /// @param token The token to borrow (must be token0 or token1 of the pool)\n    /// @param amount Amount to borrow\n    /// @param userData Your opaque data forwarded to the callback and _afterFlashLoan\n    function _requestUniswapV3(\n        address pool,\n        address token,\n        uint256 amount,\n        bytes memory userData\n    ) internal authorizeFlashLoan(pool) {\n        require(uniswapEnabled(), \"Uniswap v3 disabled\");\n        require(pool != address(0), \"Pool required\");\n        require(amount > 0, \"Amount=0\");\n\n        address t0 = IUniswapV3Pool(pool).token0();\n        address t1 = IUniswapV3Pool(pool).token1();\n        bool is0 = token == t0;\n        bool is1 = token == t1;\n        require(is0 || is1, \"Token not in pool\");\n\n        // Encode pool + token orientation so callback can verify msg.sender and repay the correct leg.\n        bytes memory data = abi.encode(pool, token, amount, userData, is0);\n\n        emit FlashRequested(\"UNISWAP_V3\", token, amount);\n        IUniswapV3Pool(pool).flash(address(this), is0 ? amount : 0, is1 ? amount : 0, data);\n    }\n\n    /// @notice Uniswap v3 flash callback. Must repay (amount + fee) to the *pool* (msg.sender).\n    function uniswapV3FlashCallback(\n        uint256 fee0,\n        uint256 fee1,\n        bytes calldata data\n    ) external flashLoanOnly {\n        (address pool, address token, uint256 amount, bytes memory userData, bool is0) =\n            abi.decode(data, (address, address, uint256, bytes, bool));\n\n        require(msg.sender == pool, \"Unauthorized Uniswap pool\");\n\n        uint256 fee = is0 ? fee0 : fee1;\n        uint256 repayment = amount + fee;\n        _afterFlashLoan(token, repayment, userData);\n\n        IERC20(token).safeTransfer(msg.sender, repayment);\n        emit FlashRepaid(\"UNISWAP_V3\", token, repayment);\n    }\n\n    // ============================================================\n    //                       ABSTRACT RESUME HOOK\n    // ============================================================\n\n    /// @dev Derived contracts implement how to resume execution (swap/repay/move collateral etc.).\n    /// @param token The token address that was borrowed\n    /// @param repaymentAmount The total amount that must be repaid (principal + fee)\n    /// @param userData Your opaque data forwarded from the request\n    ///      Accepts bytes memory to support both calldata (from callbacks) and memory (from decoded data).\n    function _afterFlashLoan(address token, uint256 repaymentAmount, bytes memory userData) internal virtual;\n}\n\n\n"
    },
    "contracts/v2/interfaces/balancer/IFlashLoanProvider.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.30;\n\nimport {IERC20} from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\ninterface IFlashLoanProvider {\n    function flashLoan(\n        address receiver,\n        IERC20[] calldata tokens,\n        uint256[] calldata amounts,\n        bytes calldata userData\n    ) external;\n}\n\n\n"
    },
    "contracts/v2/interfaces/balancer/IVaultV3.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.30;\n\ninterface IVaultV3 {\n    function unlock(bytes calldata data) external returns (bytes memory result);\n    function sendTo(address token, address to, uint256 amount) external;\n    function settle(address token, uint256 amount) external;\n}\n\n\n"
    },
    "contracts/v2/interfaces/IGateway.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.30;\n\nimport {ProtocolTypes} from \"./ProtocolTypes.sol\";\n\ninterface IGateway {\n    function processLendingInstruction(ProtocolTypes.Output[] calldata inputs, bytes calldata data)\n        external\n        returns (ProtocolTypes.Output[] memory outputs);\n\n    /// @notice Return user-side authorization calls for the given lending instructions.\n    ///         One element per input (or (address(0), bytes(\"\")) if none needed).\n    ///         Also returns the produced outputs from the simulation of these instructions.\n    function authorize(\n        ProtocolTypes.LendingInstruction[] calldata instrs,\n        address caller,\n        ProtocolTypes.Output[] calldata inputs\n    ) external view returns (\n        address[] memory targets,\n        bytes[] memory data,\n        ProtocolTypes.Output[] memory produced\n    );\n\n    /// @notice Return user-side deauthorization calls for the given lending instructions.\n    ///         Used to revoke permissions after execution.\n    function deauthorize(\n        ProtocolTypes.LendingInstruction[] calldata instrs,\n        address caller\n    ) external view returns (address[] memory targets, bytes[] memory data);\n}\n\n\n"
    },
    "contracts/v2/interfaces/ProtocolTypes.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.30;\n\nlibrary ProtocolTypes {\n    struct ProtocolInstruction {\n        string protocolName;\n        bytes data;\n    }\n\n    struct Output {\n        address token;\n        uint256 amount;\n    }\n\n    // Common lending instruction schema for gateways (deposit/withdraw/borrow/repay)\n    enum LendingOp { Deposit, DepositCollateral, WithdrawCollateral, Borrow, Repay, GetBorrowBalance, GetSupplyBalance }\n    struct InputPtr { uint256 index; }\n    /**\n     * Cross-protocol instruction.\n     * - For Compound you can pass `context = abi.encode(address marketBaseToken)`\n     *   which tells the gateway which Comet (market) to use. Empty means infer from token.\n     */\n    struct LendingInstruction {\n        LendingOp op;\n        address token;        // underlying being acted on (base or collateral)\n        address user;         // end user on whose account we operate\n        uint256 amount;       // amount in underlying units\n        bytes context;        // (optional) protocol-specific blob (e.g., Compound marketBaseToken)\n        InputPtr input;       // (optional) pointer to prior output (UTXO); use if index < inputs.length\n    }\n}\n\n\n"
    },
    "contracts/v2/KapanRouter.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.30;\n\nimport { Ownable } from \"@openzeppelin/contracts/access/Ownable.sol\";\nimport { ReentrancyGuard } from \"@openzeppelin/contracts/utils/ReentrancyGuard.sol\";\nimport { SafeERC20 } from \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { TBytes } from \"./libraries/TBytes.sol\";\nimport { IGateway } from \"./interfaces/IGateway.sol\";\nimport { ProtocolTypes } from \"./interfaces/ProtocolTypes.sol\";\nimport { FlashLoanConsumerBase } from \"./flashloans/FlashLoanConsumerBase.sol\";\n\ncontract KapanRouter is Ownable, ReentrancyGuard, FlashLoanConsumerBase {\n    using SafeERC20 for IERC20;\n\n    bytes32 constant INSTRUCTION_STACK = keccak256(\"KapanRouter:instructionStack\");\n    bytes32 constant OUTPUTS_SLOT = keccak256(\"KapanRouter:outputs\");\n    mapping(string => IGateway) public gateways;\n\n    constructor(address owner) Ownable(owner) {}\n\n    function addGateway(string calldata protocolName, address gateway) external onlyOwner {\n        if (address(gateways[protocolName]) == gateway) {\n            return;\n        }\n        require(address(gateways[protocolName]) == address(0), \"Gateway already exists\");\n        gateways[protocolName] = IGateway(gateway);\n    }\n\n    function setBalancerV2(address provider) external onlyOwner {\n        _setBalancerV2(provider);\n    }\n\n    function setBalancerV3(address vault) external onlyOwner {\n        _setBalancerV3(vault);\n    }\n\n    function setAaveV3(address pool) external onlyOwner {\n        _setAaveV3(pool);\n    }\n\n    function setUniswapV3Enabled(address factoryOrSentinel) external onlyOwner {\n        _setUniswapV3Enabled(factoryOrSentinel);\n    }\n\n    enum RouterInstructionType {\n        FlashLoan,\n        PullToken,\n        PushToken,\n        ToOutput,\n        Approve\n    }\n    enum FlashLoanProvider {\n        BalancerV2,\n        BalancerV3,\n        AaveV3,\n        UniswapV3\n    }\n    struct RouterInstruction {\n        uint256 amount;\n        address token;\n        address user;\n        RouterInstructionType instructionType;\n    }\n\n    function convertToStack(ProtocolTypes.ProtocolInstruction[] calldata instructions) internal {\n        ProtocolTypes.ProtocolInstruction[] memory reversed = new ProtocolTypes.ProtocolInstruction[](\n            instructions.length\n        );\n\n        // Iterate in reverse order to build the stack (top is last)\n        for (uint256 i = 0; i < instructions.length; i++) {\n            reversed[i] = instructions[instructions.length - 1 - i];\n        }\n\n        TBytes.set(INSTRUCTION_STACK, abi.encode(reversed));\n    }\n\n    function popStack() internal returns (ProtocolTypes.ProtocolInstruction memory instruction, bool isEmpty) {\n        bytes memory raw = TBytes.get(INSTRUCTION_STACK);\n        if (raw.length == 0) {\n            return (instruction, true);\n        }\n        ProtocolTypes.ProtocolInstruction[] memory reversed = abi.decode(raw, (ProtocolTypes.ProtocolInstruction[]));\n        uint256 len = reversed.length;\n        if (len == 0) {\n            return (instruction, true);\n        }\n        instruction = reversed[len - 1];\n        assembly {\n            mstore(reversed, sub(len, 1))\n        }\n        TBytes.set(INSTRUCTION_STACK, abi.encode(reversed));\n        isEmpty = (len - 1 == 0);\n    }\n\n    function processProtocolInstructions(ProtocolTypes.ProtocolInstruction[] calldata instructions) external {\n        verifyInstructionAuthorization(instructions);\n        convertToStack(instructions);\n        runStack();\n        deleteOutputs();\n    }\n\n    function deleteOutputs() internal {\n        // Reset the output slot to empty\n        TBytes.set(OUTPUTS_SLOT, bytes(\"\"));\n    }\n\n    function verifyInstructionAuthorization(ProtocolTypes.ProtocolInstruction[] calldata instructions) internal view {\n        for (uint256 i = 0; i < instructions.length; i++) {\n            ProtocolTypes.ProtocolInstruction calldata instruction = instructions[i];\n            // Skip router instructions (they have their own authorization)\n            if (keccak256(abi.encode(instruction.protocolName)) != keccak256(abi.encode(\"router\"))) {\n                ProtocolTypes.LendingInstruction memory lendingInstr = abi.decode(\n                    instruction.data,\n                    (ProtocolTypes.LendingInstruction)\n                );\n                if (\n                    lendingInstr.op == ProtocolTypes.LendingOp.Borrow ||\n                    lendingInstr.op == ProtocolTypes.LendingOp.WithdrawCollateral\n                ) {\n                    require(lendingInstr.user == msg.sender, \"Not authorized: sender must match user\");\n                }\n            }\n        }\n    }\n\n    function runStack() internal {\n        (ProtocolTypes.ProtocolInstruction memory instruction, bool isEmpty) = popStack();\n        if (bytes(instruction.protocolName).length == 0) {\n            return;\n        }\n        uint256 instructionIndex = 0;\n        while (true) {\n            ProtocolTypes.Output[] memory currentOutputs = _getOutputs();\n\n            if (keccak256(abi.encode(instruction.protocolName)) == keccak256(abi.encode(\"router\"))) {\n                bool halt = processRouterInstruction(instruction);\n                if (halt) {\n                    return;\n                }\n            } else {\n                IGateway gw = gateways[instruction.protocolName];\n                if (address(gw) == address(0)) {\n                    revert(\"Gateway not found\");\n                }\n                ProtocolTypes.Output[] memory inputs = _getOutputs();\n                ProtocolTypes.Output[] memory produced = gw.processLendingInstruction(inputs, instruction.data);\n                if (produced.length > 0) {\n                    _appendOutputs(produced);\n                }\n            }\n\n            ProtocolTypes.Output[] memory outputsAfter = _getOutputs();\n\n            if (isEmpty) {\n                break;\n            }\n            instructionIndex++;\n            (instruction, isEmpty) = popStack();\n        }\n    }\n\n    function processRouterInstruction(\n        ProtocolTypes.ProtocolInstruction memory instruction\n    ) internal returns (bool halt) {\n        RouterInstruction memory routerInstruction = abi.decode(instruction.data, (RouterInstruction));\n        if (routerInstruction.instructionType == RouterInstructionType.FlashLoan) {\n            // instruction.data encodes: (RouterInstruction, FlashLoanProvider, InputPtr, address pool)\n            // pool is only used for UniswapV3, otherwise address(0)\n            (, FlashLoanProvider provider, ProtocolTypes.InputPtr memory inputPtr, address pool) = abi.decode(\n                instruction.data,\n                (RouterInstruction, FlashLoanProvider, ProtocolTypes.InputPtr, address)\n            );\n            processFlashLoan(provider, inputPtr, pool);\n            return true; // halt to wait for callback\n        } else if (routerInstruction.instructionType == RouterInstructionType.PullToken) {\n            processPullToken(routerInstruction);\n        } else if (routerInstruction.instructionType == RouterInstructionType.PushToken) {\n            processPushToken(instruction);\n        } else if (routerInstruction.instructionType == RouterInstructionType.ToOutput) {\n            ProtocolTypes.Output[] memory out = new ProtocolTypes.Output[](1);\n            out[0] = ProtocolTypes.Output({ token: routerInstruction.token, amount: routerInstruction.amount });\n            _appendOutputs(out);\n        } else if (routerInstruction.instructionType == RouterInstructionType.Approve) {\n            // instruction.data encodes: (RouterInstruction, string targetProtocol, InputPtr input)\n            (, string memory targetProtocol, ProtocolTypes.InputPtr memory inputPtr) = abi.decode(\n                instruction.data,\n                (RouterInstruction, string, ProtocolTypes.InputPtr)\n            );\n            ProtocolTypes.Output[] memory inputs = _getOutputs();\n            require(inputPtr.index < inputs.length, \"Approve: bad index\");\n            address target;\n            if (keccak256(abi.encode(targetProtocol)) == keccak256(abi.encode(\"router\"))) {\n                target = address(this);\n            } else {\n                target = address(gateways[targetProtocol]);\n            }\n            require(target != address(0), \"Approve: target not found\");\n            IERC20(inputs[inputPtr.index].token).approve(target, 0);\n            IERC20(inputs[inputPtr.index].token).approve(target, inputs[inputPtr.index].amount);\n            // Always produce an output (even if empty) to ensure consistent indexing\n            ProtocolTypes.Output[] memory out = new ProtocolTypes.Output[](1);\n            out[0] = ProtocolTypes.Output({ token: address(0), amount: 0 });\n            _appendOutputs(out);\n        }\n        return false;\n    }\n\n    function processFlashLoan(\n        FlashLoanProvider provider,\n        ProtocolTypes.InputPtr memory inputPtr,\n        address pool\n    ) internal {\n        // Read the amount and token from the output stack using InputPtr\n        ProtocolTypes.Output[] memory inputs = _getOutputs();\n        require(inputPtr.index < inputs.length, \"FlashLoan: bad index\");\n        ProtocolTypes.Output memory input = inputs[inputPtr.index];\n        require(input.token != address(0), \"FlashLoan: zero token\");\n        require(input.amount > 0, \"FlashLoan: zero amount\");\n\n        // Route to the appropriate provider\n        if (provider == FlashLoanProvider.BalancerV2) {\n            _requestBalancerV2(input.token, input.amount, bytes(\"\"));\n        } else if (provider == FlashLoanProvider.BalancerV3) {\n            _requestBalancerV3(input.token, input.amount);\n        } else if (provider == FlashLoanProvider.AaveV3) {\n            _requestAaveV3(input.token, input.amount, bytes(\"\"));\n        } else if (provider == FlashLoanProvider.UniswapV3) {\n            require(pool != address(0), \"FlashLoan: UniswapV3 requires pool address\");\n            _requestUniswapV3(pool, input.token, input.amount, bytes(\"\"));\n        } else {\n            revert(\"FlashLoan: unsupported provider\");\n        }\n    }\n\n    function processPullToken(RouterInstruction memory routerInstruction) internal authorize(routerInstruction) {\n        IERC20(routerInstruction.token).safeTransferFrom(msg.sender, address(this), routerInstruction.amount);\n        ProtocolTypes.Output[] memory out = new ProtocolTypes.Output[](1);\n        out[0] = ProtocolTypes.Output({ token: routerInstruction.token, amount: routerInstruction.amount });\n        _appendOutputs(out);\n    }\n\n    function processPushToken(ProtocolTypes.ProtocolInstruction memory instruction) internal {\n        // instruction.data encodes: (RouterInstruction, ProtocolTypes.InputPtr input)\n        (, ProtocolTypes.InputPtr memory inputPtr) = abi.decode(\n            instruction.data,\n            (RouterInstruction, ProtocolTypes.InputPtr)\n        );\n        ProtocolTypes.Output[] memory inputs = _getOutputs();\n        require(inputPtr.index < inputs.length, \"PushToken: bad index\");\n        ProtocolTypes.Output memory output = inputs[inputPtr.index];\n        require(output.token != address(0), \"PushToken: zero token\");\n        // Extract user from RouterInstruction in data\n        if (output.amount != 0) {\n            RouterInstruction memory routerInstruction;\n            (routerInstruction, ) = abi.decode(instruction.data, (RouterInstruction, ProtocolTypes.InputPtr));\n            IERC20(output.token).safeTransfer(routerInstruction.user, output.amount);\n        }\n        // Consume the UTXO by clearing it (set to zero)\n        inputs[inputPtr.index] = ProtocolTypes.Output({ token: address(0), amount: 0 });\n        TBytes.set(OUTPUTS_SLOT, abi.encode(inputs));\n    }\n\n    modifier authorize(RouterInstruction memory routerInstruction) {\n        require(routerInstruction.user == msg.sender, \"Not authorized\");\n        _;\n    }\n\n    function _afterFlashLoan(address token, uint256 repaymentAmount, bytes memory /*userData*/) internal override {\n        // Create UTXO from flash loan with the repayment amount (principal + fee)\n        // This ensures refinancing can borrow exactly what's needed to repay\n        ProtocolTypes.Output[] memory out = new ProtocolTypes.Output[](1);\n        out[0] = ProtocolTypes.Output({ token: token, amount: repaymentAmount });\n        _appendOutputs(out);\n        runStack();\n    }\n\n    // --------- Router-side authorize encoder ---------\n    function authorizeRouter(\n        RouterInstruction[] calldata instrs\n    ) external view returns (address[] memory targets, bytes[] memory data) {\n        uint256 count;\n        for (uint256 i = 0; i < instrs.length; i++) {\n            if (instrs[i].instructionType == RouterInstructionType.PullToken) count++;\n        }\n        targets = new address[](count);\n        data = new bytes[](count);\n        uint256 k;\n        for (uint256 i = 0; i < instrs.length; i++) {\n            RouterInstruction calldata r = instrs[i];\n            if (r.instructionType == RouterInstructionType.PullToken) {\n                targets[k] = r.token;\n                data[k] = abi.encodeWithSelector(IERC20.approve.selector, address(this), r.amount);\n                k++;\n            }\n        }\n    }\n\n    // --------- Aggregate authorization for ProtocolInstructions ---------\n    function authorizeInstructions(\n        ProtocolTypes.ProtocolInstruction[] calldata instructions,\n        address caller\n    ) external view returns (address[] memory targets, bytes[] memory data) {\n        // Simulation state (outputs)\n        ProtocolTypes.Output[] memory outputs = new ProtocolTypes.Output[](0);\n\n        // Results\n        address[] memory tmpTargets = new address[](instructions.length);\n        bytes[] memory tmpData = new bytes[](instructions.length);\n        uint256 k;\n\n        for (uint256 i = 0; i < instructions.length; i++) {\n            ProtocolTypes.ProtocolInstruction calldata pi = instructions[i];\n\n            // Router step\n            if (keccak256(abi.encode(pi.protocolName)) == keccak256(abi.encode(\"router\"))) {\n                RouterInstruction memory r = abi.decode(pi.data, (RouterInstruction));\n\n                // Default no auth\n                tmpTargets[k] = address(0);\n                tmpData[k] = bytes(\"\");\n\n                if (r.instructionType == RouterInstructionType.PullToken) {\n                    if (r.user == caller) {\n                        uint256 current = IERC20(r.token).allowance(caller, address(this));\n                        if (current < r.amount) {\n                            tmpTargets[k] = r.token;\n                            tmpData[k] = abi.encodeWithSelector(IERC20.approve.selector, address(this), r.amount);\n                        }\n                    }\n                    outputs = _appendOutputMemory(outputs, ProtocolTypes.Output({ token: r.token, amount: r.amount }));\n                } else if (r.instructionType == RouterInstructionType.PushToken) {\n                    (, ProtocolTypes.InputPtr memory inputPtr) = abi.decode(\n                        pi.data,\n                        (RouterInstruction, ProtocolTypes.InputPtr)\n                    );\n                    if (inputPtr.index < outputs.length) {\n                        outputs[inputPtr.index] = ProtocolTypes.Output({ token: address(0), amount: 0 });\n                    }\n                } else if (r.instructionType == RouterInstructionType.ToOutput) {\n                    outputs = _appendOutputMemory(outputs, ProtocolTypes.Output({ token: r.token, amount: r.amount }));\n                } else if (r.instructionType == RouterInstructionType.Approve) {\n                    outputs = _appendOutputMemory(outputs, ProtocolTypes.Output({ token: address(0), amount: 0 }));\n                } else if (r.instructionType == RouterInstructionType.FlashLoan) {\n                    // Simulate flashloan output WITH BUFFER for fees\n                    (, , ProtocolTypes.InputPtr memory inputPtr, ) = abi.decode(\n                        pi.data,\n                        (RouterInstruction, FlashLoanProvider, ProtocolTypes.InputPtr, address)\n                    );\n                    if (inputPtr.index < outputs.length) {\n                        ProtocolTypes.Output memory loanOut = outputs[inputPtr.index];\n                        // 0.1% Buffer for Flash Loan Fee simulation\n                        // This ensures Repay instructions downstream request enough allowance\n                        loanOut.amount = (loanOut.amount * 1001) / 1000;\n                        outputs = _appendOutputMemory(outputs, loanOut);\n                    } else {\n                        outputs = _appendOutputMemory(outputs, ProtocolTypes.Output({ token: address(0), amount: 0 }));\n                    }\n                }\n\n                k++;\n                continue;\n            }\n\n            // Protocol step\n            IGateway gw = gateways[pi.protocolName];\n\n            if (address(gw) == address(0)) {\n                tmpTargets[k] = address(0);\n                tmpData[k] = bytes(\"\");\n                k++;\n                continue;\n            }\n\n            ProtocolTypes.LendingInstruction memory li = abi.decode(pi.data, (ProtocolTypes.LendingInstruction));\n\n            ProtocolTypes.LendingInstruction[] memory one = new ProtocolTypes.LendingInstruction[](1);\n            one[0] = li;\n\n            // Call authorize with simulation inputs\n            (address[] memory t, bytes[] memory d, ProtocolTypes.Output[] memory produced) = gw.authorize(\n                one,\n                caller,\n                outputs\n            );\n\n            if (t.length > 0 && t[0] != address(0) && d[0].length > 0) {\n                tmpTargets[k] = t[0];\n                tmpData[k] = d[0];\n            } else {\n                tmpTargets[k] = address(0);\n                tmpData[k] = bytes(\"\");\n            }\n\n            // Update simulation state\n            if (produced.length > 0) {\n                outputs = _concatOutputsMemory(outputs, produced);\n            }\n\n            k++;\n        }\n\n        // Compact\n        targets = new address[](k);\n        data = new bytes[](k);\n        for (uint256 i = 0; i < k; i++) {\n            targets[i] = tmpTargets[i];\n            data[i] = tmpData[i];\n        }\n    }\n\n    function deauthorizeInstructions(\n        ProtocolTypes.ProtocolInstruction[] calldata instructions,\n        address caller\n    ) external view returns (address[] memory targets, bytes[] memory data) {\n        address[] memory tmpTargets = new address[](instructions.length);\n        bytes[] memory tmpData = new bytes[](instructions.length);\n        uint256 k;\n\n        for (uint256 i = 0; i < instructions.length; i++) {\n            ProtocolTypes.ProtocolInstruction calldata pi = instructions[i];\n\n            if (keccak256(abi.encode(pi.protocolName)) == keccak256(abi.encode(\"router\"))) {\n                RouterInstruction memory r = abi.decode(pi.data, (RouterInstruction));\n                if (r.instructionType == RouterInstructionType.PullToken && r.user == caller) {\n                    tmpTargets[k] = r.token;\n                    tmpData[k] = abi.encodeWithSelector(IERC20.approve.selector, address(this), 0);\n                } else {\n                    tmpTargets[k] = address(0);\n                    tmpData[k] = bytes(\"\");\n                }\n                k++;\n                continue;\n            }\n\n            IGateway gw = gateways[pi.protocolName];\n            if (address(gw) != address(0)) {\n                ProtocolTypes.LendingInstruction memory li = abi.decode(pi.data, (ProtocolTypes.LendingInstruction));\n                ProtocolTypes.LendingInstruction[] memory one = new ProtocolTypes.LendingInstruction[](1);\n                one[0] = li;\n\n                (address[] memory t, bytes[] memory d) = gw.deauthorize(one, caller);\n                if (t.length > 0 && t[0] != address(0)) {\n                    tmpTargets[k] = t[0];\n                    tmpData[k] = d[0];\n                } else {\n                    tmpTargets[k] = address(0);\n                    tmpData[k] = bytes(\"\");\n                }\n            } else {\n                tmpTargets[k] = address(0);\n                tmpData[k] = bytes(\"\");\n            }\n            k++;\n        }\n\n        targets = new address[](k);\n        data = new bytes[](k);\n        for (uint256 i = 0; i < k; i++) {\n            targets[i] = tmpTargets[i];\n            data[i] = tmpData[i];\n        }\n    }\n\n    function _getOutputs() internal view returns (ProtocolTypes.Output[] memory out) {\n        bytes memory raw = TBytes.get(OUTPUTS_SLOT);\n        if (raw.length == 0) return new ProtocolTypes.Output[](0);\n        return abi.decode(raw, (ProtocolTypes.Output[]));\n    }\n\n    function _appendOutputs(ProtocolTypes.Output[] memory produced) internal {\n        ProtocolTypes.Output[] memory cur = _getOutputs();\n        ProtocolTypes.Output[] memory merged = new ProtocolTypes.Output[](cur.length + produced.length);\n        for (uint256 i = 0; i < cur.length; i++) {\n            merged[i] = cur[i];\n        }\n        for (uint256 j = 0; j < produced.length; j++) {\n            merged[cur.length + j] = produced[j];\n        }\n        TBytes.set(OUTPUTS_SLOT, abi.encode(merged));\n    }\n\n    function _appendOutputMemory(\n        ProtocolTypes.Output[] memory current,\n        ProtocolTypes.Output memory item\n    ) internal pure returns (ProtocolTypes.Output[] memory) {\n        ProtocolTypes.Output[] memory next = new ProtocolTypes.Output[](current.length + 1);\n        for (uint i = 0; i < current.length; i++) next[i] = current[i];\n        next[current.length] = item;\n        return next;\n    }\n\n    function _concatOutputsMemory(\n        ProtocolTypes.Output[] memory current,\n        ProtocolTypes.Output[] memory items\n    ) internal pure returns (ProtocolTypes.Output[] memory) {\n        ProtocolTypes.Output[] memory next = new ProtocolTypes.Output[](current.length + items.length);\n        for (uint i = 0; i < current.length; i++) next[i] = current[i];\n        for (uint j = 0; j < items.length; j++) next[current.length + j] = items[j];\n        return next;\n    }\n}\n"
    },
    "contracts/v2/libraries/TBytes.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.30;\n\n/// @notice Minimal transient bytes encoder/decoder.\n/// Layout at `base`:\n///   base:    uint256 length in bytes\n///   base+1  base+N: data words (ceil(len/32) words)\nlibrary TBytes {\n    /// @dev Store `src` at `base` (overwrites previous content). Tail bytes are zero-masked.\n    function set(bytes32 base, bytes memory src) internal {\n        assembly {\n            let len := mload(src)\n            tstore(base, len)\n\n            let ptr := add(src, 0x20)\n            let full := div(len, 32)\n            let rem := mod(len, 32)\n\n            // store full words\n            for { let i := 0 } lt(i, full) { i := add(i, 1) } {\n                tstore(add(base, add(1, i)), mload(add(ptr, mul(i, 32))))\n            }\n\n            // store last partial (masked) word\n            if rem {\n                let last := mload(add(ptr, mul(full, 32)))\n                // keep top `rem` bytes, zero the low (32 - rem) bytes\n                let mask := not(sub(exp(256, sub(32, rem)), 1))\n                last := and(last, mask)\n                tstore(add(base, add(1, full)), last)\n            }\n            // Note: we do not need to clear old extra words; reads use the exact byte length.\n        }\n    }\n\n    /// @dev Load bytes previously written at `base`.\n    function get(bytes32 base) internal view returns (bytes memory out) {\n        assembly {\n            let len := tload(base)\n            out := mload(0x40)\n            mstore(out, len)\n\n            let dst := add(out, 0x20)\n            let full := div(len, 32)\n            let rem := mod(len, 32)\n\n            for { let i := 0 } lt(i, full) { i := add(i, 1) } {\n                mstore(add(dst, mul(i, 32)), tload(add(base, add(1, i))))\n            }\n            if rem {\n                mstore(add(dst, mul(full, 32)), tload(add(base, add(1, full))))\n            }\n\n            // bump free memory pointer to ceil32(len)\n            mstore(0x40, add(dst, and(add(len, 31), not(31))))\n        }\n    }\n\n    /// @dev Number of data words currently stored (ceil(len/32)).\n    function wordCount(bytes32 base) internal view returns (uint256 words) {\n        uint256 len;\n        assembly { len := tload(base) }\n        unchecked { words = (len + 31) >> 5; }\n    }\n\n    /// @dev Clear the region (length and words).\n    function clear(bytes32 base) internal {\n        assembly {\n            let len := tload(base)\n            let words := add(div(len, 32), iszero(iszero(mod(len, 32))))\n            tstore(base, 0)\n            for { let i := 0 } lt(i, words) { i := add(i, 1) } {\n                tstore(add(base, add(1, i)), 0)\n            }\n        }\n    }\n}\n\n\n"
    },
    "contracts/v2/test/TestKapanRouter.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.30;\n\nimport {KapanRouter} from \"../KapanRouter.sol\";\nimport {ProtocolTypes} from \"../interfaces/ProtocolTypes.sol\";\n\ncontract TestKapanRouter is KapanRouter {\n    constructor(address owner) KapanRouter(owner) {}\n\n    function seedStackForTest(ProtocolTypes.ProtocolInstruction[] calldata instructions) external onlyOwner {\n        convertToStack(instructions);\n    }\n}\n\n\n"
    }
  },
  "settings": {
    "evmVersion": "cancun",
    "optimizer": {
      "enabled": true,
      "runs": 200
    },
    "viaIR": true,
    "outputSelection": {
      "*": {
        "*": [
          "abi",
          "evm.bytecode",
          "evm.deployedBytecode",
          "evm.methodIdentifiers",
          "metadata",
          "devdoc",
          "userdoc",
          "storageLayout",
          "evm.gasEstimates"
        ],
        "": [
          "ast"
        ]
      }
    },
    "metadata": {
      "useLiteralContent": true
    }
  }
}